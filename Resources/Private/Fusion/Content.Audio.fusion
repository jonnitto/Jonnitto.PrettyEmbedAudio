prototype(Jonnitto.PrettyEmbedAudio:Content.Audio) < prototype(Jonnitto.PrettyEmbed:Content) {
    @propTypes {
        // These properties are already defined in Jonnitto.PrettyEmbed:Content
        wrapper = ${PropTypes.anyOf(PropTypes.string, PropTypes.boolean)}
        label = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
        metadata = ${PropTypes.shape({
            'duration': PropTypes.integer
        })}
    }

    player = 'Audio'

    audioFileTypes = ${['mp3', 'wave', 'ogg']}
    assets = ${q(node).property('assets')}
    hasExternalSwitch = ${q(node).is('[instanceof Jonnitto.PrettyEmbedAudio:Mixin.Sources]')}
    external = ${q(node).property('external')}
    externalMp3 = ${q(node).property('externalMp3')}
    externalWave = ${q(node).property('externalWave')}
    externalOgg = ${q(node).property('externalOgg')}

    disableAudioPlayer = ${node.context.inBackend}

    # This enumerated attribute is intended to provide a hint to the browser about what
    # the author thinks will lead to the best user experience with regards to what content
    # is loaded before the video is played. It may have one of the following values:
    #  - none       Indicates that the video should not be preloaded.
    #  - metadata   Indicates that only video metadata (e.g. length) is fetched.
    #  - auto       Indicates that the whole video file can be downloaded, even if the user is not expected to use it
    preload = Jonnitto.PrettyEmbed:GetProperty {
        property = 'preload'
    }

    # https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attr-crossorigin
    crossorigin = Jonnitto.PrettyEmbed:GetProperty {
        property = 'crossorigin'
    }

    # If true, the browser will offer controls to allow the user to control
    # audio playback, including volume, seeking, and pause/resume playback.
    controls = Jonnitto.PrettyEmbed:GetProperty {
        property = 'controls'
    }

    # If true, the browser will automatically seek back
    # to the start upon reaching the end of the audio.
    loop = Jonnitto.PrettyEmbed:GetProperty {
        property = 'loop'
    }

    renderer = Neos.Fusion:Component {
        @apply {
            props = ${props}
            overwrite = Neos.Fusion:DataStructure {
                assets = Neos.Fusion:Loop {
                    items = ${props.audioFileTypes}
                    itemName = 'fileextension'
                    itemRenderer = Neos.Fusion:Loop {
                        items = ${props.assets}
                        itemRenderer = Jonnitto.PrettyEmbed:Presentation.Source {
                            type = 'audio'
                            asset = ${item}
                            fileextension = ${fileextension}
                        }
                    }
                }

                external = Neos.Fusion:Loop {
                    items = Neos.Fusion:DataStructure {
                        mp3 = ${Type.isString(props.externalMp3) ? String.trim(props.externalMp3) : null}
                        wave = ${Type.isString(props.externalWave) ? String.trim(props.externalWave) : null}
                        ogg = ${Type.isString(props.externalOgg) ? String.trim(props.externalOgg) : null}
                        @process.filter = ${Array.filter(value)}
                    }
                    itemKey = 'fileextension'
                    itemRenderer = Neos.Fusion:Loop {
                        // Split by line break
                        items = ${String.pregSplit(item, '/\r?\n/')}
                        itemRenderer = Jonnitto.PrettyEmbed:Presentation.Source {
                            type = 'audio'
                            uri = ${String.trim(item)}
                            fileextension = ${fileextension}
                        }
                    }
                }
                sources = ${props.hasExternalSwitch ? (props.external ? this.external : this.assets) : this.assets + this.external}
            }
        }

        renderer = Neos.Fusion:Case {
            noAudio {
                condition = ${!props.sources}
                renderer = Jonnitto.PrettyEmbed:Notification {
                    key = 'noAudio'
                    default = 'Please add an audio file or url in the inspector'
                    class = ${props.class}
                }
            }
            default {
                condition = true
                renderer = Jonnitto.PrettyEmbed:Presentation.Audio {
                    wrapper = ${props.wrapper}
                    label = ${props.label}
                    id = ${props.id}
                    controls = ${props.controls}
                    loop = ${props.loop}
                    preload = ${props.preload}
                    crossorigin = ${props.crossorigin}
                    content = ${props.sources}
                }
            }
        }
    }
}
