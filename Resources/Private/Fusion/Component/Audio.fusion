prototype(Jonnitto.PrettyEmbedAudio:Component.Audio) < prototype(Neos.Fusion:Component) {
    configuration = ${Configuration.setting('Jonnitto.PrettyEmbedAudio')}

    controls = ${this.configuration.defaults.controls}
    loop = ${this.configuration.defaults.loop}
    preload = ${this.configuration.defaults.preload}
    playsinline = ${this.configuration.defaults.playsinline}
    crossorigin = ${this.configuration.defaults.crossorigin}
    class = ${null}
    audioClass = ${null}
    disableAudioPlayer = ${null}

    wrapper = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.wrapper')}

    id = ${null}

    sources = ${null}

    content = ${this.sources}

    renderer = afx`
        <div class={[BEM.modifier('jonnitto-prettyembed', 'audio'), props.class]}>
            <Jonnitto.PrettyEmbedHelper:Assets />
            <audio
                id={props.id}
                controls={props.controls}
                loop={props.loop}
                preload={props.preload}
                playsinline={props.playsinline}
                crossorigin={props.crossorigin}
                class={props.audioClass}
                style={props.disableAudioPlayer ? 'pointer-events:none' : null}
            >
                {props.content}
            </audio>
        </div>
    `
    renderer.@process.wrapper = Jonnitto.PrettyEmbedHelper:Wrapper {
        wrapper = ${props.wrapper}
    }
}
