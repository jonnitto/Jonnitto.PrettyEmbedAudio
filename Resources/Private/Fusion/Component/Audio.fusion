prototype(Jonnitto.PrettyEmbedAudio:Component.Audio) < prototype(Neos.Fusion:Component) {
    configuration = ${Configuration.setting('Jonnitto.PrettyEmbedAudio')}

    controls = ${this.configuration.defaults.controls}
    loop = ${this.configuration.defaults.loop}
    preload = ${this.configuration.defaults.preload}
    playsinline = ${this.configuration.defaults.playsinline}
    crossorigin = ${this.configuration.defaults.crossorigin}
    class = null
    audioClass = null

    wrapper = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.wrapper')}

    id = null

    sources = null

    content = ${this.sources}

    renderer = afx`
        <div class={[BEM.modifier('jonnitto-prettyembed', 'audio'), props.class]}>
            <audio
                id={props.id}
                controls={props.controls}
                loop={props.loop}
                preload={props.preload}
                playsinline={props.playsinline}
                crossorigin={props.crossorigin}
                class={props.audioClass}
            >
                {props.content}
            </audio>
        </div>
    `
    renderer.@process.wrapper = Jonnitto.PrettyEmbedHelper:Wrapper {
        wrapper = ${props.wrapper}
    }
}
